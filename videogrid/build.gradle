plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'maven-publish'
    id 'kotlin-parcelize'
    id 'signing'
    id "org.jetbrains.dokka" version "1.5.0"
}

def getVersionName = { ->
    return "$HMS_ROOM_KIT_VERSION"
}

android {
    namespace 'live.hms.videogrid'
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33

        versionCode 1
        versionName getVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
        dataBinding {
            enabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

dependencies {
    def hmsVersion = "2.7.7-de"
    // To add dependencies of specific module
    implementation "live.100ms:android-sdk:$hmsVersion"
    implementation "live.100ms:video-view:$hmsVersion"


    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'androidx.fragment:fragment-ktx:1.3.2'

    // Life Cycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.4.0"
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.0'
    implementation 'com.google.android.material:material:1.5.0'

    implementation "live.100ms:common:0.0.6"
}


task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveFileName = "javadoc.jar"
    from "build/dokka/javadoc"
    archiveClassifier.set("javadoc")
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                pom {
                    // Only sign the artefacts if this is a maven central build.
                    // This would only halt jitpack builds and/or make our signing keys public.
                    if(rootProject.properties["ossrhUsername"]) {
                        artifact javadocJar
                        signing {
                            sign publishing.publications
                        }
                    }
                    name = "100ms.live Android Room Kit"
                    description = "Room Kit that simplifies setting up videoconferencing in your own app. See more at https://www.100ms.live/docs/android/v2/guides/quickstart"
                    url = "100ms.live"
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'http://www.opensource.org/licenses/mit-license.php'
                        }
                    }
                    developers {
                        developer {
                            id = '1'
                            name = '100ms'
                            email = 'support@100ms.live'
                        }
                    }
                    scm {
                        connection = 'SCM is private'
                        developerConnection = 'SCM is private'
                        url = 'SCM is private'
                    }
                }
                groupId = "live.100ms"
                artifactId = 'videogrid'
                version = getVersionName()
            }
        }
    }
}